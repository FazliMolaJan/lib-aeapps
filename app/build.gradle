apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        
        versionCode 37
        versionName "2.2.0"

        archivesBaseName = "lib-ae-apps"
        version = android.defaultConfig.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    
    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}-${variant.buildType.name}.aar"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    // Tasks for Generating JavaDocs
    libraryVariants.all { variant ->
        task("generate${variant.name}Javadoc", type: Javadoc) {
            description "Generates Javadoc for $variant.name."
            source = variant.javaCompiler.source
            title = "LibAeApps " + android.defaultConfig.versionName
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
            exclude '**/inapp/*.java'
            exclude '**/billing/*.java'
            failOnError false
            ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            classpath = files(variant.javaCompiler.classpath.files) + files(ext.androidJar)
            destinationDir = file("${project.projectDir}/../docs")
        }
    }
    
    lintOptions {
        abortOnError false
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.ae.apps'
    PUBLISH_ARTIFACT_ID = 'android-libraries'
    PUBLISH_VERSION = '2.2.0'
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
    }
}

dependencies {
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'

    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-core:1.10.19'
}

apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'
